#!/usr/bin/perl
#
# Copyright (c) 2025 joshua stein <jcs@jcs.org>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

sub on_child_start {
	my ($self, $pid) = @_;
	$self->{shell_pid} = $pid;
}

sub on_wm_delete_window {
	my ($self, $event) = @_;

	# find tty of shell pid
	my @tty = map { my $f = "/dev/" . $_; chomp($f); $f }
		grep { !/^TTY/ }
		`ps -o tty -p $self->{shell_pid}`;

	# find any other pids on this tty other than the shell
	my @running = map { /^(\d+) (.+)/ ? $2 : () }
		grep { /^\d+ / && !/^$self->{shell_pid} / }
		`ps -t $tty[0] -o pid,command`;

	if ($#running > -1) {
		open my $fh, "-|",
			"Xdialog",
			"--center",
			"--title", "urxvt",
			"--cancel-label", "No",
			"--ok-label", "Yes",
			"--yesno",
			($#running == 0 ? "Process" : "Processes") .
				" still running here, close anyway?\\n\\n" .
				join("\\n", @running),
			scalar (12 + ($#running * 3)),
			"100"
			or die $!;
		my $ret = close($fh);
		if ($ret == 1) {
			# successful close, i pressed ok, close window
			return 0;
		}
		return 1;
	}

	return 0;
}
